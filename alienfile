use alienfile;

plugin 'PkgConfig' => 'libzmq';

# You can control installed version of ZMQ by setting environment variable.
# Linux: export ALIEN_ZMQ_VERSION=3.*
# Win: set ALIEN_ZMQ_VERSION=3.*
#
my $zmq_version = $ENV{ALIEN_ZMQ_VERSION} || '([\d\.]+)';

# http://zeromq.org/
share {
	plugin Download => (
		url => 'http://download.zeromq.org',
		version => qr/zeromq-$zmq_version\.tar\.gz/
	);

	plugin Extract => 'tar.gz';

	plugin 'Build::Autoconf';
	my $enable_static = $^O ne 'MSWin32';

	# Remove tests from SUBDIRS
	# * We don't need test programs.
	# * Some targets cannot be built on strawberry perl.
	patch sub {
	    unless (-f "Makefile.in.org") {
		rename "Makefile.in", "Makefile.in.org";
	    }
	    open my $ifh, "<", "Makefile.in.org" or die "$!";
	    open my $ofh, ">", "Makefile.in" or die "$!";
	    while (<$ifh>) {
		chomp;
		
		if (/(.*DIRS =)(.*) tests((:? .*)?)/) {
		    my ($defvar, $head, $tail) = ($1, $2, $3);
		    print "defvar = $defvar, head = $head, tail = $tail\n";
		    my $line = "$defvar$head$tail";
		    print "< $_\n";
		    print "> $line\n";
		    print $ofh "$line\n";
		}
		else {
		    print $ofh "$_\n";
		}
	    }
	};

	build [
		join(' ', (
			'%{configure}',
			$enable_static ? '--enable-static' : '--disable-static',
			'--enable-shared'
		)),
		'%{make}',
		'%{make} install',
	];

	plugin 'Gather::Dino';
};
